buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
	}
}

apply plugin: 'forge'

repositories {
	maven {
		name = "squiddev"
		url = "http://maven.bonzodandd.co.uk"
	}

	ivy {
		name = "ComputerCraft"
		artifactPattern "http://addons-origin.cursecdn.com/files/2228/723/[module][revision].[ext]"
	}
}

configurations {
	fatPackage
}

group = "org.squiddev"
archivesBaseName = "CCTweaks"
version = mc_version + "-" + mod_version

minecraft {
	version = project.mc_version + "-" + project.forge_version
	runDir = "eclipse"

	replace '${mod_version}', project.mod_version
	replace '${mc_version}', project.mc_version
}

dependencies {
	compile group: 'ComputerCraft', name: 'ComputerCraft', version: "${project.cc_version}", ext: 'jar'
	fatPackage(group: 'org.squiddev', name: 'luaj.luajc', version: '1.+') {
		// We provide a custom LuaJ source with ComputerCraft
		exclude group: 'org.ow2.asm'
		exclude group: 'org.luaj'
	}

	testCompile group: "junit", name: "junit", version: "4.+"
}
sourceSets.main.compileClasspath += [configurations.fatPackage]

idea.module.scopes.PROVIDED.plus += [configurations.fatPackage]
eclipse.classpath.plusConfigurations += [configurations.fatPackage]

jar {
	manifest {
		attributes(
			'FMLCorePlugin': 'org.squiddev.cctweaks.core.asm.TweaksLoadingPlugin',
			'FMLCorePluginContainsFMLMod': 'true'
		)
	}

	// Package all the org.squiddev dependencies into one file
	from configurations.fatPackage
		.findAll { it.getAbsolutePath().contains("org.squiddev") }
		.collect { it.isDirectory() ? it : zipTree(it) }

	// Package the patch files into a patch/ folder
	filesMatching("org/luaj/**", {
		it.path = "patch/" + it.path
	})
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "mod_version", project.mod_version
	inputs.property "mc_version", project.mc_version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// Replace mod_version and mc_version
		expand 'mod_version': mod_version, 'mc_version': mc_version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}
